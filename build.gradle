plugins {
    id 'kotlin-multiplatform' version '1.3.40'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url = uri("https://kotlin.bintray.com/ktor") }
}

group 'com.tmik'
version '0.0.3'

apply plugin: 'maven-publish'

kotlin {
    ext.ktor_version = '1.2.1'
    ext.coroutines_version = "1.3.0-M1"

    jvm()
    js {
        compilations.main.kotlinOptions {
            moduleKind = 'commonjs'
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                implementation "io.ktor:ktor-client-websockets:$ktor_version"
                implementation "io.ktor:ktor-network:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')

                implementation "io.ktor:ktor-client-cio:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "ch.qos.logback:logback-classic:1.2.3"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')

                implementation "io.ktor:ktor-client-js:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

bintray {
    user = hasProperty("teamcity") ? teamcity['btUser'] : "N/A"
    key = hasProperty("teamcity") ? teamcity['btKey'] : "N/A"
    override = true

    pkg {
        repo = 'maven'
        name = 'TmiK'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/wooodenleg/TmiK.git'
        issueTrackerUrl = 'https://github.com/wooodenleg/TmiK/issues'

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it.name
    }
}