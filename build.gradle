plugins {
    id 'kotlin-multiplatform' version '1.3.40'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
}

group 'com.tmik'
version '0.0.9-exp3'

apply plugin: 'maven-publish'

kotlin {
    ext.ktor_version = '1.2.1'
    ext.coroutines_version = "1.3.0-M1"

    jvm()
    js {
        compilations.main.kotlinOptions {
            moduleKind = 'commonjs'
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "com.squareup.okhttp3:okhttp:3.14.2"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation(npm("ws", "7.0.1"))
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

bintray {
    user = project.hasProperty('btUser') ? btUser : "N/A"
    key = project.hasProperty('btKey') ? btKey : "N/A"

    pkg {
        repo = 'maven'
        name = project.hasProperty('btPackage') ? btPackage : "N/A"
        licenses = ['MIT']
        vcsUrl = 'https://github.com/wooodenleg/TmiK.git'
        issueTrackerUrl = 'https://github.com/wooodenleg/TmiK/issues'

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it.name
    }
}