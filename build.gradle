plugins {
    id 'kotlin-multiplatform' version '1.4.20'
    id "com.jfrog.bintray" version "1.8.5"
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'maven-publish'

kotlin {
    ext.ktor_version = '1.4.2'
    ext.coroutines_version = "1.4.2"

    jvm()
    js {
        browser()
        nodejs()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "com.squareup.okhttp3:okhttp:4.9.0"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation(npm("ws", "7.4.0"))
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

def GROUP_ID= "com.tmik"
def ARTIFACT_ID = "TmiK"

afterEvaluate {
    project.publishing.publications.all {
        // rename artifacts
        groupId = GROUP_ID
        if (it.name.contains('metadata')) {
            artifactId = "$ARTIFACT_ID"
        } else {
            artifactId = "$ARTIFACT_ID-$name"
        }
    }
}


bintray {
    user = System.getenv('btUser') ?: "N/A"
    key = System.getenv('btKey') ?: "N/A"

    override = true

    pkg {
        repo = 'maven'
        name = System.getenv('btPackage') ?: "N/A"
        licenses = ['MIT']
        vcsUrl = 'https://github.com/wooodenleg/TmiK.git'
        issueTrackerUrl = 'https://github.com/wooodenleg/TmiK/issues'

        version {
            name = System.getenv('btVersion')
            vcsTag = System.getenv('btVersion')
            released = new Date()
        }
    }
}

task initProject {
    println "Initializing project ${GROUP_ID}:${System.getenv('btVersion')}"
    version System.getenv('btVersion')
    group GROUP_ID
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }.findAll {
        it != "kotlinMultiplatform"
    }
}

publishToMavenLocal.dependsOn initProject
bintrayUpload.dependsOn publishToMavenLocal
